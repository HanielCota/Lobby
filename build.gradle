plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.hanielfialho'
version = '1.0-SNAPSHOT'
tasks.build.dependsOn 'shadowJar'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.imanity.dev/imanity-libraries/'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'

    //api for minecraft
    compileOnly 'org.imanity.paperspigot:paper1.8.8:1.8.8'

    //caffeine cache
    implementation "com.github.ben-manes.caffeine:caffeine:3.1.8"

    //connection-pool
    implementation 'com.zaxxer:HikariCP:5.1.0'

    //command-framework for spigot
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    //annotations processor
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

}

compileJava {
    options.encoding = "UTF-8"
}


def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}


tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
